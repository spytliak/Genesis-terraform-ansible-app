#-----------------------------------------------------------------------
# Random password for rds
#-----------------------------------------------------------------------
resource "random_password" "password_rds" {
  count = var.rds["create_pass"] ? 1:0 
  length           = 12
  special          = true
  override_special = "!$%&*()-_=+:?"
}

#-----------------------------------------------------------------------
# Module
#-----------------------------------------------------------------------
# AWS RDS 
#-----------------------------------------------------------------------
module "db" {
  source  = "terraform-aws-modules/rds/aws"
  version = "~> 4.4.0"

  depends_on = [
    module.vpc_genesis,
    random_password.password_rds
  ]

  identifier = "rds-genesis"

  engine         = "mysql"
  engine_version = "8.0.29"
  instance_class = "db.t3.micro"

  allocated_storage       = 5
  backup_retention_period = 0
  deletion_protection     = false
  skip_final_snapshot     = true
  
  create_random_password = false

  port     = var.rds["db_port"]
  db_name  = var.rds["db_name"]
  username = var.rds["db_user"]
  password = var.rds["create_pass"] ? random_password.password_rds[0].result : var.rds["db_pass"]

  iam_database_authentication_enabled = false
  create_monitoring_role              = false

  create_db_subnet_group = true
  subnet_ids             = module.vpc_genesis.private_subnet_ids
  vpc_security_group_ids = [
    aws_security_group.rds.id
  ]

  tags = merge(var.common_tags, { Name = "RDS-${var.project}-${var.env}" })

  family               = "mysql8.0"
  major_engine_version = "8.0"

  parameters = [
    {
      name  = "character_set_client"
      value = "utf8mb4"
    },
    {
      name  = "character_set_server"
      value = "utf8mb4"
    }
  ]

  options = [
    {
      option_name = "MARIADB_AUDIT_PLUGIN"

      option_settings = [
        {
          name  = "SERVER_AUDIT_EVENTS"
          value = "CONNECT"
        },
        {
          name  = "SERVER_AUDIT_FILE_ROTATIONS"
          value = "37"
        }
      ]
    }
  ]
}
